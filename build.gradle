plugins {
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
}

group = 'com.tonkar'
version = '3.2'
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    def jjwt_version = '0.12.5'
    def lombok_version = '1.18.30'
    def jsoup_version =  '1.17.2'
    def mail_version = '2.0.1'
    def google_oauth2_version = '1.19.0'
    def ehcache_version = '3.10.8'
    def android_publisher_version = 'v3-rev20230815-2.0.0'
    def poi_xml_version = '5.2.3'
    def testcontainers_version = '1.19.7'
    def faker_version = '2.1.0'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation group: 'org.jsoup', name: 'jsoup', version: jsoup_version
    implementation group: 'com.sun.mail', name: 'jakarta.mail', version: mail_version
    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: jjwt_version
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: jjwt_version
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: jjwt_version
    // Caching
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation group: 'org.ehcache', name: 'ehcache', version: ehcache_version
    // Google APIs
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: google_oauth2_version
    implementation group: 'com.google.apis', name: 'google-api-services-androidpublisher', version: android_publisher_version
    // Excel
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: poi_xml_version
    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version
    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: testcontainers_version
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainers_version
    testImplementation group: 'org.testcontainers', name: 'mongodb', version: testcontainers_version
    testImplementation group: 'net.datafaker', name: 'datafaker', version: faker_version
}

test {
    useJUnitPlatform()
}
